# .zshrc
# ======
# vim: set sts=2 sw=2 ts=2

# To profile zsh startup, use the following:
# In ~/.zshrc,
#  (BEGIN) zmodload zsh/zprof
#  (END)   zprof >! ~/.zsh.startup.log
# % repeat 3 {time zsh -i -c exit}


# zgen           {{{
# ===================
if [[ ! -s "${ZDOTDIR:-$HOME}/.zgen/zgen.zsh" ]]; then
  echo -e "\033[0;31m zgen is not installed; please update dotfiles !"
  echo -e "\033[0;33m  e.g. $ dotfiles update\n\
       $ cd ~/.dotfiles && python install.py"
  echo -e "\033[0m"
  return
fi

source "${ZDOTDIR:-$HOME}/.zgen/zgen.zsh"


# Source the Prezto configuration file.
if [[ -s "${ZDOTDIR:-$HOME}/.zpreztorc" ]]; then
  source "${ZDOTDIR:-$HOME}/.zpreztorc"
fi

# Dirty hacks for Prezto+zplug
# @see https://github.com/zplug/zplug/issues/373
zstyle ":prezto:module:completion" loaded 'yes'

# Do not put all usernames as possible candidates for tab completion.
zstyle ':completion:*' users root $USER


# virtualenvwrapper -- use lazy load (see prezto#669)
if (( $+commands[virtualenvwrapper_lazy.sh] )); then
    source "$commands[virtualenvwrapper_lazy.sh]"
fi

# nvm -- sourcing is very slow, use lazy load
if [[ -f "$HOME/.nvm/nvm.sh" ]]; then
  function nvm() {
    source "$HOME/.nvm/nvm.sh"
    nvm "$@"
  }
fi

# Additional zplug from ~/.zshrc.local
if [[ -s "${ZDOTDIR:-$HOME}/.zshrc.local" ]]; then
  source "${ZDOTDIR:-$HOME}/.zshrc.local"
fi

# Misc plugin configs {{{
# -----------------------

# fzf-marks
export FZF_MARKS_JUMP='^b'

# }}}

# ------------------------------------------------------ }}}

# zgen plugin specifications
# NOTE: To reflect the added/updated plugins, try 'zgen reset'
if ! zgen saved; then
  echo "Initializing zgen plugins ..."

  # load prezto. see ~/.zpreztorc for plugin configurations
  zgen prezto

  # additional modules/plugins
  # --------------------------

  # zsh theme: pure (forked ver)
  zgen load mafredri/zsh-async
  zgen load wookayin/pure

  # zsh syntax: FSH
  # theme file (XDG:wook) is at ~/.config/fsh/wook.ini
  # For some reason, @zdharma's repo is gone -- workaround by using its fork
  rm -rf ~/.zgen/zdharma/fast-syntax-highlighting-master
  zgen load wookayin/fast-syntax-highlighting
  fast-theme "XDG:wook"

  # More completion support
  zgen load esc/conda-zsh-completion

  # misc.
  # #see ~/.zsh/zsh.d/envs.zsh for configs
  zgen load wookayin/fzf-fasd

  # fzf-marks
  zgen load urbainvaes/fzf-marks

  zgen load zsh-users/zsh-autosuggestions
  if (( $+commands[virtualenvwrapper_lazy.sh] || $+commands[conda] )); then
    # Use my own fork for a while, to support autoswitch into anaconda envs
    zgen load wookayin/zsh-autoswitch-virtualenv
  fi
  if [[ "`uname`" == "Darwin" ]]; then
    zgen load wookayin/anybar-zsh
  fi

  zgen save

fi

# }}} ===================


# Source after-{zplug,zgen} zsh script files.
for config_file (${ZDOTDIR:-$HOME}/.zsh/zsh.d/*.zsh(N)) source $config_file

# Terminal
# Use xterm-256color (for tmux, too)
export TERM="xterm-256color"

# iTerm integration (for OS X iTerm2)
# @see https://iterm2.com/shell_integration.html
if [[ "`uname`" == "Darwin" ]] && [[ -f ${HOME}/.iterm2_shell_integration.zsh ]]; then
    source ${HOME}/.iterm2_shell_integration.zsh
fi

if (( $+commands[iterm-tab-color] )); then
    # set tab color, if it is a new connection to remote through SSH
    function iterm_tab_color_auto() {
        if [ -z "$TMUX" ] && [ -n "$SSH_CONNECTION" ] && [ -n "$PROMPT_HOST_COLOR" ]; then
            iterm-tab-color $PROMPT_HOST_COLOR
        fi
    }
    iterm_tab_color_auto
fi

# Anaconda3
# see ~/.zshenv for $CONDA_EXE detection
# >>> conda initialize >>>
if [ -n "${CONDA_EXE}" ]; then
  ${CONDA_EXE} config --set auto_activate_base false
  __conda_setup="$(${CONDA_EXE} 'shell.zsh' 'hook' 2> /dev/null)"
  if [ $? -eq 0 ]; then
      eval "$__conda_setup"
  fi
  unset __conda_setup
fi
# <<< conda initialize <<<

# fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
