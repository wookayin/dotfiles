#
# Defines environment variables.
#
# See also: ~/.zsh/zsh.d/envs.zsh
#
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Jongwook Choi <wookayin@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL='nvim'
else
  export EDITOR='vim'
  export VISUAL='vim'
fi

export PAGER='less'

# Unset GIT_EDITOR (usually nvr) inside a neovim-floaterm session.
# See ~/.vimrc: $GIT_EDITOR section for more details.
if [[ -n "$FLOATERM" ]]; then
  unset GIT_EDITOR
fi

# See https://github.com/neovim/neovim/issues/22717
# There is really no reason for $VIM and $VIMRUNTIME need to be set on zsh.
if [[ -o interactive ]]; then
  unset VIM
  unset VIMRUNTIME
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

# Fix broken, wrong LC variables (e.g. kitty)
if [[ "$LC_CTYPE" == "UTF-8" ]]; then
  export LC_CTYPE='en_US.UTF-8'
fi

#
# Paths
#

typeset -gU cdpath fpath mailpath path

# [For Mac M1 USERS]
# Homebrew: /opt/homebrew/bin must precede /usr/local/bin.
# Please update /etc/paths: add `/opt/homebrew/bin` manually.

if ! (( $path[(I)/opt/homebrew/bin] )); then
    # Add homebrew bin path to $path automatically, if
    # /etc/paths is not sourced or not configured.
    path=($path /opt/homebrew/bin)
fi

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
# see ~/.zprofile as well
path=(
  $path
  /usr/local/{bin,sbin}
)

# Let ~/.local/bin take precedence
if ! (( ${path[(I)$HOME/.local/bin]} )); then
  path=( $HOME/.local/bin $path )
fi

# Additional $PATH configuration:

# dotfiles-populated bin.
if [ -d $HOME/.dotfiles/bin/ ]; then
  path=( $path $HOME/.dotfiles/bin )
fi

# Node + Yarn
if [ -d $HOME/.yarn/bin/ ]; then
  path=( $path $HOME/.yarn/bin )
fi

# Ruby on macos (M1)
# Homebrew ruby is keg-only, no symlink into /opt/homebrew/bin,
# so we'll need manually add ruby and gem bins to PATH
# NOTE: We hvae to pin an exact ruby version (brew install ruby@3.0)
if [[ "$OSTYPE" == darwin* && "$(uname -m)" == "arm64" ]]; then
  path=(
    "/opt/homebrew/opt/ruby@3.0/bin"
    "/opt/homebrew/lib/ruby/gems/3.0.0/bin"
    $path
  )
fi

# Cleanup some conda environment variables to avoid messing up in tmux, etc.
# (these environment variables might be copied and inherited unwantedly)
unset CONDA_EXE
unset CONDA_PREFIX
unset CONDA_DEFAULT_ENV
unset CONDA_PYTHON_EXE
unset CONDA_SHLVL

# conda (miniforge3, miniconda3)
function _try_conda_base() {
  local conda_base="$1"
  [[ -z "$conda_base" ]] && return 1;
  if [[ -n "$CONDA_EXE" ]]; then
    return 0;  # do nothing, CONDA_EXE is already found
  fi

  if [ -d "$conda_base" ]; then
    path=( $path "$conda_base/bin" )
    export CONDA_EXE="$conda_base/bin/conda"
    if [ ! -f "$CONDA_EXE" ]; then
      echo "Warning: $CONDA_EXE does not exist"
    fi
  fi
}

_try_conda_base "$HOME/.miniforge3";
_try_conda_base "$HOME/miniforge3";
_try_conda_base "$HOME/.miniconda3";
_try_conda_base "$HOME/miniconda3";
_try_conda_base "/usr/local/miniconda3";
unfunction _try_conda_base

# rust (cargo)
if [ -d $HOME/.cargo/bin/ ]; then
  path=( $path $HOME/.cargo/bin )
fi


#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
if (( $+commands[lesspipe.sh] )); then
  export LESSOPEN='| /usr/bin/env lesspipe.sh %s 2>&-'
fi

#
# Temporary Files
#

if [[ -d "$TMPDIR" ]]; then
  export TMPPREFIX="${TMPDIR%/}/zsh"
  if [[ ! -d "$TMPPREFIX" ]]; then
    mkdir -p "$TMPPREFIX"
  fi
fi


#
# Add custom config directory.
#  (note that this line is executed before initialization of prezto.)
#
fpath=(${ZDOTDIR:-$HOME}/.zsh/prezto-themes ~/.zsh/functions ~/.local/share/zsh/site-functions $fpath)


#
# Python - Virtualenv, etc.
#

export PYTHONBREAKPOINT='pudb.set_trace'

if [[ "$(uname)" == "Darwin" ]]; then
    # Mac OS X: use python shipped by Homebrew for virtualenv.
    export VIRTUALENVWRAPPER_PYTHON='/usr/local/bin/python'
else
    # use default system python for virtualenv.
    export VIRTUALENVWRAPPER_PYTHON='/usr/bin/python'
fi

# Set the directory where virtual environments are stored.
export WORKON_HOME="$HOME/.virtualenvs"
export VIRTUAL_ENV_DISABLE_PROMPT=1

# ensure that all new virtual environments are isolated from the system site-packages.
# (--no-site-packages has been removed since virtualenv >= 20, which has been the default option)
# export VIRTUALENVWRAPPER_VIRTUALENV_ARGS='--no-site-packages'

# python startup
export PYTHONSTARTUP=$HOME/.pythonrc.py


# Disable dot files in archive
export COPYFILE_DISABLE=true

if [ -f "$HOME/.zshenv.local" ]; then
  source "$HOME/.zshenv.local"
fi
